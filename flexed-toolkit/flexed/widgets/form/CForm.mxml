<?xml version="1.0" encoding="utf-8"?>

<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="initCForm()" xmlns="*" width="100%" height="100%">
	<mx:Metadata>
		/**
		 *  CFormLoadComplete is dispatched after all the controls are created
		 *  and the properties array is created. 
		 *
		 *  @eventType mx.events.DynamicEvent
		 *
		 *  @see mx.events.DynamicEvent
		 */		 
		[Event(name="CFormLoadComplete",type="mx.events.DynamicEvent")]
	</mx:Metadata>
    <mx:Script>
	<![CDATA[
		import flexed.widgets.form.custom.SectionTitle;
		import flexed.widgets.form.meta.MetaHelper;
		
		import mx.controls.ComboBox;
		import mx.accessibility.ComboBoxAccImpl;
		import mx.containers.Grid;
		import mx.containers.GridItem;
		import mx.containers.GridRow;
		import mx.validators.ValidationResult;
		import mx.collections.ArrayCollection;
		import mx.events.ValidationResultEvent;
		import mx.validators.StringValidator;
		import mx.formatters.DateFormatter;
		import mx.controls.Button;
		import mx.controls.List;
		import mx.controls.VRule;
		import mx.containers.Form;
		import mx.controls.HRule;
		import mx.containers.FormHeading;
		import mx.validators.Validator;
		import mx.validators.NumberValidator;
		import mx.controls.CheckBox;
		import mx.controls.TextInput;
		import mx.controls.FormItemLabel;
		import mx.controls.Label;
		import mx.controls.DataGrid;
		import mx.containers.FormItem;
		import mx.core.UIComponent;
		import mx.controls.Alert;
		import mx.rpc.AsyncToken;
		import mx.rpc.AbstractOperation;
		import mx.rpc.soap.WebService;
		import mx.rpc.events.*;
		import mx.events.DynamicEvent;
		
		public var widgets:Object;
		public var widgetsFile:String;
		public var widgetsXML:XML;
		public var fieldType:Array = new Array;
		public var originalData:Array = new Array;
		public var gridColumns:Array = new Array;
		public var columns:int;
				
		private var _customizeCForm:Function;
		private var _updateCForm:Function;
		private var _indicatorGap:int = 14;
		private var _xmlContent:XML = new XML();
		/**
		 *  Initializes the CForm. Loads the widgets xml file.
		 *
		 *  Called on <i>creationComplete</i>.
		 * 
		 */
		public function initCForm():void {
			if(widgetsFile != null) createCForm(widgetsFile);
		}		
		
		/**
		 *  Sets a custom function specified by the caller. This
		 *  function can be used to manipulate fields in the CForm. 
		 *
		 *  @param customizer The custom function located in the caller, 
		 *  which will be fired on the CForm
		 *
		 */
		public function set cFormCustomizer(customizer:Function):void {
			this._customizeCForm = customizer;
		}
		
		/**
		 *  Returns the custom function (if there is one) specified by the caller.
		 *
		 *  @return The custom function
		 *
		 */
		public function get cFormCustomizer():Function {
			return _customizeCForm;
		}

		/**
		 *  Need to document this.
		 *
		 */
		public function set cFormUpdater(updater:Function):void {
			this._updateCForm = updater;
		}

		/**
		 *  Need to document this.
		 *
		 */
		public function get cFormUpdater():Function {
			return _updateCForm;
		}

		/**
		 *  Returns the entire xml content 
		 *  of the controls xml.
		 *
		 *  @return XML from the controls xml
		 *
		 */
		public function get xmlContent():XML {
			return _xmlContent;
		}

		/**
		 *  Need to document this.
		 *  Custom validation for the form. This will be 
		 *  used in <code>validateCForm</code>
		 *
		 */
		public function set cFormValidation(validatorFunction:Function):void {
			_customizeCForm = validatorFunction;
		}
		
		/**
		 *  This returns all the values from the form. The original values 
		 *  that were set by the caller.
		 *
		 *  @return Original values from the form. 
		 *
		 */
		public function fetchCFormValues():Array {
			return originalData;
		}

		/**
		 *  @private
		 *  This returns all the values from the form. The original values 
		 *  that were set by the caller.
		 *
		 *  @return Original values from the form. 
		 *
		 */	
		private function createCForm(widgetsFile:String): void {
			var loader:URLLoader = new URLLoader();
			loader.addEventListener(Event.COMPLETE, loadFromXML);
			
			var request:URLRequest = new URLRequest(widgetsFile);
			loader.load(request);
		}

		/**
		 *  @private
		 *  Fired on Event.COMPLETE  of URLLoader in createCForm
		 *
		 *  @param event Original values from the form. 
		 *
		 */	
		private function loadFromXML(event:Event): void { 
			var loader:URLLoader = URLLoader(event.target);
			widgetsXML = new XML(loader.data);
			_xmlContent = widgetsXML;
			layoutCForm(widgetsXML);
		}

		/**
		 *  @private
		 *  Renders all UI components in specified group and column.
		 *
		 *  @param widgetsXML  XML component created based on user 
		 *  specified field xml file. It will have the list of fields 
		 *  grouped together to display with the columns.
		 *
		 *  @event <i>CFormLoadComplete eventObjectClassName [description]</i> 
		 *
		 */			
		private function layoutCForm(widgetsXML:XML): void {
			var alertFlag:Boolean = false;		
			var label:String  = widgetsXML.descendants("label");
			var cols :String = widgetsXML.descendants("columns").toString();
			var verticalgap:String = widgetsXML.descendants("verticalgap").toString();
			var indigap:String = widgetsXML.descendants("indicatorgap").toString();

			this.label = label.toString();

			if (cols != "")
				this.columns = int(cols);
			else
				this.columns = 0;

			var vgap:int=5;

			if (verticalgap!=null && verticalgap!="")
				vgap=int(verticalgap);

			if (indigap!=null && indigap!="")
				_indicatorGap=int(indigap);
			
			var colCount:int;
			for (colCount = 0; colCount < this.columns; colCount++) {
				var grid:Grid = new Grid();
				grid.id = grid + colCount.toString();
				grid.setStyle("verticalGap", vgap);
				grid.percentHeight = 100;
				grid.percentWidth = 100;
				cont.addChild(grid);
				gridColumns[colCount+1] = grid;
			}

			var groups:XMLList = widgetsXML.descendants("group");
			var group:XML;
			widgets = new Object();
			
			for each(group in groups) {
				var whichColumn:String = group.attribute("column").toString();
				var currentColumn:int;

				if (whichColumn != "")
					currentColumn = int(whichColumn);
				else
					currentColumn = 0;

				if(currentColumn > 0 && currentColumn <= columns){
					layoutCFormGroup(group, currentColumn);
				}else{
					if(alertFlag != true){
						Alert.show("Error in the columns specified.","Screen Layout Error");
						alertFlag = true;
					}
				}		
			}

			var event:DynamicEvent=new DynamicEvent("CFormLoadComplete");
			dispatchEvent(event);
		}

		/**
		 *  @private
		 *  Renders UI components of a group in the specified column.
		 *
		 *  @param groupXML  Fields falling under a group.
		 *  @param currentColumn Column number for layout.
		 *
		 */	
		private function layoutCFormGroup(groupXML:XML, currentColumn:int):void {
			var rows:XMLList=groupXML.descendants("row");
			var row:XML;

			var eachWidget:Array = new Array();
			var name:String = groupXML.attribute("name").toString();

			var grid:Grid = gridColumns[currentColumn];

			var gTitleItem:GridItem=null;
			var gHruleItem:GridItem=null;
			
			if (name != '') {
				var title:Label = new Label();
				title.text = name;
				title.styleName = "viewTitle";
				
				var hRule:HRule = new HRule();
				hRule.percentWidth = 100;
				hRule.styleName = "viewTitleHRule";
				
				var gTitlerow:GridRow=new GridRow();				
				gTitleItem=new GridItem();
				gTitleItem.addChild(title);
				gTitleItem.colSpan=2;
				gTitleItem.percentWidth=98;
				gTitlerow.addChild(gTitleItem);
				
				var gHrulerow:GridRow=new GridRow();
				gHruleItem=new GridItem();
				gHruleItem.addChild(hRule);
				gHruleItem.colSpan=2;
				gHruleItem.percentWidth=98;
				gHrulerow.addChild(gHruleItem);
				
				grid.addChild(gTitlerow);
				grid.addChild(gHrulerow);
				
			}
			var maxcolspan:int=2;
			for each(row in rows) {
				var fields:XMLList = row.descendants("field"); 
				var field:XML;
				
				var gItemrow:GridRow=new GridRow();
				var colspan:int=0;
				var gItemrow1:GridRow=new GridRow();
				var hasChild : Boolean = false;
				for each(field in fields) {
	            	eachWidget = new Array();
	            	var fieldId:String = field.child("id").toString();
	            	fieldId = fieldId.replace(/ /g,"");
	            	var fieldName:String = null; 
	            	if(field.child("name").length()!=0) fieldName=field.child("name").toString();
	            	
	            	var tooltip:String = field.child("tooltip").toString();
	            	eachWidget["field"] = field;
	            	eachWidget["fieldId"] = fieldId;

					hasChild = true;
	            	var gLabel:GridItem=new GridItem();
	            	var gItemlabel:Label=new Label();
	            	gItemlabel.text=fieldName;
	            	var styleName:String="viewLabel";
	            	
	            	if (field.child("name").attribute("styleName").toString()!=""){
	            		styleName=field.child("name").attribute("styleName").toString();
	            	}

	            	if (field.child("name").attribute("height").toString()!=""){
	            		gItemlabel.height=int(field.child("name").attribute("height"));
	            	}
	            	
	            	gItemlabel.styleName=styleName;
	            	gItemlabel.toolTip=tooltip;
	            	gLabel.addChild(gItemlabel);
	            	
	            	var renderer:CFormItemRenderer = createCFormItemRenderer(fieldId, fieldName, field, fields);
	            	renderer.getUIComponent().name = fieldId;
	            	
	            	if (field.child("type").attribute("styleName").toString()!=""){
		            	renderer.getUIComponent().styleName=field.child("type").attribute("styleName").toString();
	            	}
	            	
	            	var gItemvalue:GridItem = new GridItem();
	            	gItemvalue.addChild(renderer.getUIComponent());
	            	if(renderer.getUIComponent() is SectionTitle || renderer.getUIComponent() is DataGrid){
	            		gItemvalue.colSpan = 2;
	            		gItemrow.addChild(gItemvalue);
	            	}
	            	else{
		            	if(fieldName!=null) {
		            		if (((field.child("name").attribute("enableNextrow").toString()=="true")||(groupXML.attribute("enableNextrow").toString()=="true")) && (field.child("name").attribute("enableNextrow").toString()!="false")){
		            			gLabel.colSpan=2;
		            			gItemrow.addChild(gLabel);
		            		}
		            		else{
	            				gItemrow.addChild(gLabel);
		            		}
		            		colspan++;
		            	}
		            	if (((field.child("name").attribute("enableNextrow").toString()=="true")||(groupXML.attribute("enableNextrow").toString()=="true")) && (field.child("name").attribute("enableNextrow").toString()!="false")){
	            			gItemvalue.colSpan=2;
	            	 		gItemrow1.addChild(gItemvalue);   
		            	}
		            	else {
							gItemrow.addChild(gItemvalue);
		            	}
		            	if(field.child("type").attribute("colspan").length()!=0) {
		            		var span:int=int(field.child("type").attribute("colspan").toString());
		            		gItemvalue.colSpan=span;
		            		colspan=colspan+span;
		            	}
		            	else {
		            		colspan++;
		            	}
	            	}
	            	eachWidget["formItem"] = gItemlabel;
	            	eachWidget["renderer"] = renderer;
	            	widgets[fieldId] = eachWidget;	
				}
				if(hasChild == true){
					grid.addChild(gItemrow);
					if (((field.child("name").attribute("enableNextrow").toString()=="true")
							||(groupXML.attribute("enableNextrow").toString()=="true")) 
						&& (field.child("name").attribute("enableNextrow").toString()!="false"))
							grid.addChild(gItemrow1);
				}
				maxcolspan=Math.max(maxcolspan,colspan);
			}
			if(gTitleItem!=null) gTitleItem.colSpan=maxcolspan;
			if(gHruleItem!=null) gHruleItem.colSpan=maxcolspan;			

			if (name != '') {
				var grow:GridRow=new GridRow();
				grid.addChild(grow);
			}
		}

		/**
		 *  @private
		 *  Validate the CForm using the custom 
		 *  validation function specified using <code>cFormCustomizer</code>
		 *
		 *  @return Boolean based on whether the content of 
		 *  the form pass validation or not.
		 *
		 */			
		private function validateCForm():Boolean {
			var valid:Boolean=true;
			if(_customizeCForm!=null) {
				var values:Object=getCFormValues(false);
				for (var key:String in values) {
					var result:ValidationResult=_customizeCForm(key,values[key],values);
					if(result.isError) {
						widgets[key].renderer.getUIComponent().errorString=result.errorMessage;
						valid=false;
					} else {
						widgets[key].renderer.getUIComponent().errorString="";
					}
				}
			}
			return valid;
		}

		/**
		 *  @private
		 *  Create label-control <code>FormItem</code> item in the CForm
		 *
		 *  @param id Id of the control
		 *  @param name Name of the control
		 *  @param field The XML containing the field details
		 *  @param fields An XML List of all fields
		 *
		 *  @return formItem created from the params passed.
		 *
		 */		
		private function createCFormItem(id:String, name:String, field:XML, fields:XMLList):FormItem {			
		   	var formItem:FormItem = new FormItem();
		    formItem.label = name;
		    return formItem;
		}
		
		/**
		 *  @private
		 *  Create each formItem using the renderers
		 *
		 *  @param id Id of the control
		 *  @param name Name of the control
		 *  @param field The XML containing the field details
		 *  @param fields An XML List of all fields
		 *
		 *  @return CFormItemRenderer Output of <code>renderDefaultItem</code> 
		 *
		 */			
		private function createCFormItemRenderer(id:String, name:String, field:XML, fields:XMLList):CFormItemRenderer {
        	var type:XMLList = field.child("type");
        	var typeId:String = type.attribute("id").toString();
        	fieldType[id] = typeId;
        	if(typeId == "table"){
        		var table :XMLList = field.child("table");
        		var column :XMLList = table.child("column");
        		return renderDefaultItem(typeId,type,column);
        	}
        	else{
        		return renderDefaultItem(typeId, type);
        	}
		}
		
		/**
		 *  @private
		 *  Create each formItem using default custom renderers
		 *
		 *  @param typeId Type of control
		 *  @param attributes XML List of attributes of the control
		 *  @param columns The XML List containing the column info
		 *
		 *  @return renderer Created from the passed parameters
		 *
		 */
		private function renderDefaultItem(typeId:String, attributes:XMLList, columns:XMLList= null):CFormItemRenderer {
			var renderer:CFormItemRenderer;
			if(typeId.toLocaleLowerCase() == "boolean") {
				renderer = new CFormItemRadioButton();
			} else if(typeId.toLocaleLowerCase() == "display") {
				renderer = new CFormItemDisplay();
				CFormItemDisplay(renderer).setAttributesFromXML(attributes);
			} else if(typeId.toLocaleLowerCase() == "dualdisplay") {
				renderer = new CFormItemDualDisplay();
				CFormItemDualDisplay(renderer).setAttributesFromXML(attributes);
			} else if(typeId.toLocaleLowerCase() == "numericstepper") {
				renderer = new CFormItemNumericStepper();
				CFormItemNumericStepper(renderer).setAttributesFromXML(attributes);
			} else if(typeId.toLocaleLowerCase() == "date") {
				renderer = new CFormItemDate();
				CFormItemDate(renderer).setAttributesFromXML(attributes);
			} else if(typeId.toLocaleLowerCase() == "dropdown") {
				renderer = new CFormItemCombobox();
				var tmp:XMLList=attributes.attribute("values");
				if(tmp) {
					var _tmp:Array=tmp.toString().split(".");
					if(_tmp.length>1) {
						var prettyNames:Object = MetaHelper.prettyNames(_tmp[0],_tmp[1]);
						var dataProvider:ArrayCollection=new ArrayCollection();
						for(var key:String in prettyNames) {
							var _data:Object=new Object();
							_data.label=prettyNames[key];
							_data.data=key;
							
							dataProvider.addItem(_data);
						}
						
						(CFormItemCombobox(renderer)).setDataProvider(dataProvider);
					}
				}
				CFormItemCombobox(renderer).setAttributesFromXML(attributes);
			} else if(typeId.toLocaleLowerCase() == "textarea") {
				renderer = new CFormItemTextArea();
				CFormItemTextArea(renderer).setAttributesFromXML(attributes);
			} else if(typeId.toLocaleLowerCase() == "dualtext") {
				renderer = new CFormItemDualText();
				CFormItemDualText(renderer).setAttributesFromXML(attributes);
			} else if (typeId.toLocaleLowerCase() == "table") {
				renderer = new CFormItemTable();
				CFormItemTable(renderer).setAttributesFromXML(attributes)
				CFormItemTable(renderer).setColumns(columns);
			} else if (typeId.toLocaleLowerCase() == "title") {
				renderer = new CFormItemTitle();
				CFormItemTitle(renderer).setAttributesFromXML(attributes);
			} else if (typeId.toLocaleLowerCase()=="spacer"){
				renderer = new CFormItemSpace();
				CFormItemSpace(renderer).setAttributesFromXML(attributes);
			}else {
				renderer = new CFormItemText();
				CFormItemText(renderer).setAttributesFromXML(attributes);
			}
			return renderer;
		}

		/**
		 *  @private
		 *  Set the values passed in to the controls in CForm
		 *
		 *  @param values an Object containing values 
		 *
		 */
		private function setFormValues(values:Object):void {
			var widget:Array;
			var key:String;
			for (key in widgets) {
				if (values[key] != null) {
					widgets[key]["renderer"].setValue(values[key]);
					originalData[key] = values[key];
				} else if(values[key]==null){
					if (widgets[key]["renderer"] is CFormItemDisplay)
						widgets[key]["renderer"].setValue("-");
					else if (widgets[key]["renderer"] is CFormItemText)
						widgets[key]["renderer"].setValue("");
				}
			}
		}
		
		/**
		 *  Resets the values in the controls to empty or 0 
		 *
		 */
		public function resetCForm():void {
			var widget:Array;
			var key:String;
			for (key in widgets) {
					if (widgets[key]["renderer"] is CFormItemText || widgets[key]["renderer"] is CFormItemTextArea){
						widgets[key]["renderer"].setValue("");	
					}else if (widgets[key]["renderer"] is CFormItemCombobox){
						widgets[key]["renderer"].getUIComponent().selectedIndex=0;	
					}else if (widgets[key]["renderer"] is CFormItemDisplay){
						widgets[key]["renderer"].setValue("");
					}else if (widgets[key]["renderer"] is CFormItemRadioButton){
						widgets[key]["renderer"].setValue(null);
					}else if (widgets[key]["renderer"] is CFormItemNumericStepper){
						widgets[key]["renderer"].setValue(0);
					}
				}
		}
		
		/**
		 *  Resets the value of the controls. Boolean Control will 
		 *  have none of the controls selected. Text Fields will be made empty.
		 *
		 */
		public function resetCFormItem():void{
			for each(var widget:Array in widgets) {
				var type:XMLList = widget["field"].child("type");
	        	var typeId:String = type.attribute("id").toString();
				if(typeId.toLowerCase() == "boolean"){
					widget["renderer"].setValue(null);
				}
				else if(typeId.toLowerCase() == "text"){
					widget["renderer"].setValue("");
				}
			}
		}
		
			
		/**
		 *  Set the values passed in to the controls in CForm
		 *  <span style="hide">Why is there two methods for the same purpose?</span>
		 *
		 *  @param values an Object containing values 
		 *
		 */
		public function setData(values:Object):void {
			var values:Object=new Object();
			
			var widget:Array;
			var key:String;
			for (key in widgets) {
				var value:Object = values[key];
				
				if(key.indexOf(".")!=-1) {
					var tmp:Array=key.split(".");
					var _tmp0:String=tmp[0];
					var _tmp1:String=tmp[1];
					var _tmp2:String=tmp[2];
					
					if(tmp.length>2){
						if(values[_tmp0][_tmp1]) value=values[_tmp0][_tmp1][_tmp2];					
					}else{
						if(values[_tmp0]) value=values[_tmp0][_tmp1];
					}
				}
				values[key]=value;
			}			
			setFormValues(values);
		}

		/**
		 *  Gets values from CForm 
		 *
		 *  @param onlyModifiedValues If <code>true</code> is 
		 *  passed, only modified items are returned. if <code>false</code> 
		 *  is passed, all the values are returned.
		 *
		 *  @default true
		 *
		 */		
		public function getData(onlyModifiedValues:Boolean=true):Object {
			var obj:Object=new Object();
			var values:Object=getCFormValues(onlyModifiedValues);
			
			var widget:Array;
			var key:String;
			for (key in values) {
				var value:Object = values[key];
				if(key.indexOf(".")!=-1) {
					var tmp:Array=key.split(".");
					if(!obj[tmp[0]]) obj[tmp[0]]=new Object();
					if(tmp.length>2){
						if(!obj[tmp[0]][tmp[1]]) obj[tmp[0]][tmp[1]]=new Object();
						obj[tmp[0]][tmp[1]][tmp[2]]=value;
					}else
					{
					obj[tmp[0]][tmp[1]]=value;
					}
				} else {
					obj[key]=value;
				}
			}
			return obj;
		}		

		/**
		 *  Called by <code>getData</code>. Soon <code>getCFormValues</code> will be made private 
		 *  and all external calls for <code>getCFormValues</code>
		 *	replaced by <code>getData</code>
		 * 
		 *  @param onlyModifiedValues If <code>true</code> is 
		 *  passed, only modified items are returned. if <code>false</code> 
		 *  is passed, all the values are returned.
		 *
		 *  @default true		 
		 *
		 */
		public function getCFormValues(onlyModifiedValues:Boolean=true): Object {
			var values:Object=new Object();

			for each(var widget:Array in widgets) {
				var fieldId:String = widget["fieldId"];
				var currentValue:Object = widget["renderer"].getValue();
				var originalValue:Object = originalData[widget["fieldId"]];
				if(currentValue == null)
					continue;
				
	        	var type:XMLList = widget["field"].child("type");
	        	var typeId:String = type.attribute("id").toString();
	        	if (typeId.toLowerCase() == "boolean" 
	        		&& (originalValue != null)) {
	        		originalValue = String(originalValue);
	        	}
	        	else if (typeId.toLowerCase() == "display"&& onlyModifiedValues)
	        		continue;
	        	else if (typeId.toLowerCase() == "table" && onlyModifiedValues)
	        	    continue;
	        	else if(typeId.toLowerCase() == "title"){
	        		continue;
	        	}
	        	else{
	        		originalValue = (originalValue == null)? "" : originalValue;
	        	}
				if(currentValue == originalValue && onlyModifiedValues) 
					continue;
				values[fieldId]=currentValue;
			}

			return values;
		}

		/**
		 *  Returns the renderer of the control with the passed 
		 *  id. This is useful, if caller wants to perform specific 
		 *  actions on select controls in CForm. Eg:- Setting 
		 *  dataproviders dynamically to combo drop downs.
		 *
		 *  @param id Id of the control
		 *
		 *  @return CFormItemRenderer If renderer is found, else return null
		 *
		 */
		public function getRenderer(id:String):CFormItemRenderer {
			if(widgets[id]) return widgets[id]["renderer"];
			return null;
		}
		
		/**
		 *  Removes all controls from the container.
		 *
		 */
		public function clearCform():void{
			cont.removeAllChildren();
		}
		
	]]>
	</mx:Script>
	<mx:VBox id="wrap" width="98%" verticalGap="0" horizontalGap="0" height="100%" scroll="false" horizontalScrollPolicy="off" paddingTop="3" paddingLeft="5">
		<mx:HBox id="cont" width="100%" horizontalGap="8" verticalGap="0" height="100%" scroll="false" horizontalScrollPolicy="off" />
	</mx:VBox>
</mx:Canvas>